plugins {
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
    id "com.modrinth.minotaur" version "2.8.7"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

version = "${project_version}-1.20.1-Forge"

minecraft {
    mappings channel: 'official', version: '1.20.1'
    copyIdeResources = true
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {}

dependencies {
    minecraft "net.minecraftforge:forge:1.20.1-${forge_20}"

    compileOnly fg.deobf("curse.maven:ftb-quests-forge-289412:${ftbquests_forge_20}")
    compileOnly fg.deobf("curse.maven:ftb-library-forge-404465:${ftblibrary_forge_20}")
    compileOnly fg.deobf("curse.maven:ftb-teams-forge-404468:${ftbteams_forge_20}")
    compileOnly fg.deobf("maven.modrinth:extralib:${extralib}-forge,1.20.1")

    annotationProcessor 'org.spongepowered:mixin:0.8.3:processor'
}

mixin {
    add sourceSets.main, 'mixins.extraquests.refmap.json'
    config 'mixins.extraquests.json'
}

processResources {
    filesMatching('META-INF/mods.toml') {
        expand(
                'project_name': project_name,
                'project_version': project_version
        )
    }
}

def gradleProperties = new Properties()
def gradlePropertiesFile = new File(rootProject.file("/.gradle/gradle.properties").path)

if (gradlePropertiesFile.exists()) {
    gradlePropertiesFile.withInputStream { gradleProperties.load(it) }
}

modrinth {
    token = gradleProperties.getProperty("MODRINTH_API_KEY")
    projectId = "extraquests"
    versionName = "${project_name}-${version}"
    versionNumber = project_version
    versionType = "release"
    changelog = rootProject.file("changelog.md").text
    uploadFile = jar
    gameVersions = ["1.20.1"]
    loaders = ["forge"]
    dependencies {
        required.project "extralib"
    }
}

jar.finalizedBy('reobfJar')
build.finalizedBy('versionedRelease')