plugins {
    id 'net.neoforged.moddev' version '2.0.119'
    id "com.modrinth.minotaur" version "2.8.10"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

version = "${project_version}-1.21.1-NeoForge"

neoForge {
    version = "${neoforge_21}"
}

dependencies {
    compileOnly "curse.maven:ftb-quests-forge-289412:${ftbquests_neoforge_21}"
    compileOnly "curse.maven:ftb-library-forge-404465:${ftblibrary_neoforge_21}"
    compileOnly "curse.maven:ftb-teams-forge-404468:${ftbteams_neoforge_21}"
    compileOnly "maven.modrinth:extralib:${extralib}-neoforge,1.21.1"
}

processResources {
    filesMatching('META-INF/neoforge.mods.toml') {
        expand(
                'project_name': project_name,
                'project_version': project_version
        )
    }
}

def gradleProperties = new Properties()
def gradlePropertiesFile = new File(rootProject.file("/.gradle/gradle.properties").path)

if (gradlePropertiesFile.exists()) {
    gradlePropertiesFile.withInputStream { gradleProperties.load(it) }
}

modrinth {
    token = gradleProperties.getProperty("MODRINTH_API_KEY")
    projectId = "extraquests"
    versionName = "${project_name}-${version}"
    versionNumber = project_version
    versionType = "release"
    changelog = rootProject.file("changelog.md").text
    uploadFile = jar
    gameVersions = ["1.21.1"]
    loaders = ["neoforge"]
    dependencies {
        required.project "extralib"
    }
}

build.finalizedBy('versionedRelease')