plugins {
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id "com.modrinth.minotaur" version "2.8.10"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

version = "${project_version}-1.12.2-Forge"

minecraft {
    mappings channel: 'stable', version: '39-1.12'
    copyIdeResources = true
}

dependencies {
    minecraft "net.minecraftforge:forge:1.12.2-${forge_12}"

    compileOnly fg.deobf("curse.maven:ftb-quests-forge-289412:${ftbquests_forge_12}")
    compileOnly fg.deobf("curse.maven:ftb-library-legacy-237167:${ftblibrary_forge_12}")
    compileOnly fg.deobf("maven.modrinth:extralib:${extralib}-forge,1.12.2")
}

processResources {
    filesMatching('mcmod.info') {
        expand(
                'project_name': project_name,
                'project_version': project_version
        )
    }
}

def gradleProperties = new Properties()
def gradlePropertiesFile = new File(rootProject.file("/.gradle/gradle.properties").path)

if (gradlePropertiesFile.exists()) {
    gradlePropertiesFile.withInputStream { gradleProperties.load(it) }
}

modrinth {
    token = gradleProperties.getProperty("MODRINTH_API_KEY")
    projectId = "extraquests"
    versionName = "${project_name}-${version}"
    versionNumber = project_version
    versionType = "release"
    changelog = rootProject.file("changelog.md").text
    uploadFile = jar
    gameVersions = ["1.12.2"]
    loaders = ["forge"]
    dependencies {
        required.project "extralib"
    }
}

jar.finalizedBy('reobfJar')
build.finalizedBy('versionedRelease')